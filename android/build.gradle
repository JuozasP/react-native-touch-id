// Inspired by rayronvictor's PR #248 tp react-native-config
// https://github.com/luggit/react-native-config/pull/248

def ext = rootProject.ext

buildscript {
  repositories {
    maven {
      url 'https://maven.google.com/'
      name 'Google'
    }
    jcenter()
    google()
  }

  dependencies {
    classpath ext.has('gradleBuildTools') ? ext.gradleBuildTools : 'com.android.tools.build:gradle:3.4.0'
  }
}

apply plugin: 'com.android.library'

def _reactNativeVersion = ext.has('reactNative') ? ext.reactNative : '+'
def _compileSdkVersion = ext.has('compileSdkVersion') ? ext.compileSdkVersion : 27
def _buildToolsVersion = ext.has('buildToolsVersion') ? ext.buildToolsVersion : '27.0.3'
def _minSdkVersion = ext.has('minSdkVersion') ? ext.minSdkVersion : 16
def _targetSdkVersion = ext.has('targetSdkVersion') ? ext.targetSdkVersion : 27

android {
  compileSdkVersion _compileSdkVersion
  buildToolsVersion _buildToolsVersion

  defaultConfig {
    minSdkVersion _minSdkVersion
    targetSdkVersion _targetSdkVersion
    versionCode 1
    versionName "1.0"
  }
  lintOptions {
    abortOnError false
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  implementation "com.facebook.react:react-native:${_reactNativeVersion}"
}
